version 1.1;

ns junos = "http://xml.juniper.net/junos/*/junos";
ns xnm = "http://xml.juniper.net/xnm/1.1/xnm";
ns jcs = "http://xml.juniper.net/junos/commit-scripts/1.0";

import "../import/junos.xsl";

var $arguments = { 
    <argument> {
        <name> "family";
        <description> "The address family to match.";
    }
}

param $family; 

template handle-logical-intf($family) {
    copy-node {
        for-each (@*) {
            attribute name(.) {
                expr .;
            }
        }
        for-each (*[name() != "address-family"]) {
            copy-of .;
        }
        for-each (address-family[address-family-name=$family]) {
            copy-of .;
        }
    }
}

template handle-physical-intf($family) {
    copy-node {
        for-each (@*) {
            attribute name(.) {
                expr .;
            }
        }
        for-each (*[name() != "logical-interface"]) {
            copy-of .;
        }
        for-each (logical-interface[address-family[
                  address-family-name=$family]]) {
            call handle-logical-intf($family=$family);
        }
    }
}

match / {
    <op-script-results> {
        var $con = jcs:open();
        var $rpc-query = {
            <get-interface-information> {
                <terse>;
            }
        }
        var $results = jcs:execute($con, $rpc-query);
        expr jcs:close($con);

        if ($family) { 
            for-each ($results) {
                copy-node {
                    for-each (@*) {
                        attribute name(.) {
                            expr .;
                        }
                    }
                    for-each (physical-interface[logical-interface/
                              address-family[address-family-name=$family]]) {
                        call handle-physical-intf($family=$family); 
                    }
                }
            }
        } else {
            copy-of $results; 
        }
    }
}
